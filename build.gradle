apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'

buildscript {
    repositories { jcenter() }
    dependencies { classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5' }
}

group = 'com.github.jruby-gradle'
version = '0.1.1'

if (System.env.RELEASE != '1') {
    version = "${version}-SNAPSHOT"
}
sourceCompatibility = '1.7'

repositories {
    jcenter()
    mavenLocal()
    maven { url 'http://dl.bintray.com/rtyler/jruby' }
}

configurations {
    testWarbler
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.github.jruby-gradle:jruby-gradle-plugin:0.1.1-SNAPSHOT'

    testCompile ("org.spockframework:spock-core:0.7-groovy-${gradle.gradleVersion.startsWith('1.')?'1.8':'2.0'}") {
        exclude module : 'groovy-all'
    }
    testWarbler group: 'com.lookout', name: 'warbler-bootstrap', version: '1.+'

}

ext {
    testWarblerDir = new File(buildDir,'tmp/test/repo' )
}

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat "full"
    }

    doFirst {
        copy {
            from project.configurations.testWarbler.files
            into testWarblerDir
        }
    }
    systemProperties TESTROOT : new File(buildDir,'tmp/test/unittests').absolutePath
    systemProperties WARBLER_LOCATION : testWarblerDir.absolutePath
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

// Ensure we don't fail in CI or on a system without these values set in
// ~/.gradle/gradle.properties
if (!hasProperty( 'bintrayUser' ))
  ext.bintrayUser = ''

if (!hasProperty( 'bintrayKey' ))
  ext.bintrayKey = ''

bintray {
    user = project.bintrayUser
    key = project.bintrayKey
    publish = true
    dryRun = false
    configurations = ['archives']

    pkg {
        userOrg = 'jruby-gradle'
        repo = 'plugins'
        name = 'jruby-gradle-jar-plugin'
        labels = ['jruby']

        version {
            name = project.version
            vcsTag = "v${project.version}"
            attributes = ['gradle-plugin' : 'com.github.jruby-gradle:com.github.jruby-gradle:jruby-gradle-jar-plugin']
            desc = 'This plugin encapsulates java archive building functionality for JRuby Gradle projects'

        }
    }
}
bintrayUpload.dependsOn assemble

// vim: ft=groovy
