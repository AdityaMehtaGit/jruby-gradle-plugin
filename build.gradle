apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'

// Pull in the bintray gradle plugin for uploading releases
buildscript {
    repositories { jcenter() }
    dependencies { classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5' }
}

group = 'com.lookout'
version = '1.1.1'

if (System.env.RELEASE != '1') {
    version = "${version}-SNAPSHOT"
}
sourceCompatibility = '1.7'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile 'junit:junit:4.+'
}

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat "full"
    }

    systemProperties TESTROOT : new File(buildDir,'tmp/test/unittests').absolutePath

}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

// Ensure we don't fail in CI or on a system without these values set in
// ~/.gradle/gradle.properties
if( !hasProperty( 'bintrayUser' ) )
  ext.bintrayUser = ''

if( !hasProperty( 'bintrayKey' ) )
  ext.bintrayKey = ''

bintray {
    user = project.bintrayUser
    key = project.bintrayKey
    publish = true
    dryRun = false
    configurations = ['archives']

    pkg {
        repo = 'jruby'
        name = 'jruby-gradle-plugin'
    }
}
bintrayUpload.dependsOn assemble

// vim: ft=groovy
